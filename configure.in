AC_INIT(sqlfs.c)


AC_CANONICAL_HOST
AC_CANONICAL_TARGET
AC_CONFIG_AUX_DIR([.])
AC_CONFIG_MACRO_DIR([m4])

AM_INIT_AUTOMAKE(libsqlfs, 1.1)

AC_PROG_CC
AC_PROG_LIBTOOL

AC_DEFINE(_GNU_SOURCE, 1)

AC_ARG_WITH([sqlcipher],
            [AS_HELP_STRING([--with-sqlcipher], [use SQLCIPHER library for client])],
            [],
            [with_sqlcipher=check])
SQLCIPHER=
AS_IF([test "x$with_sqlcipher" != xno],
	        [AC_CHECK_LIB([sqlcipher], [sqlite3_key],
             [AC_SUBST([SQLITE], ["-lsqlcipher"])
               AC_DEFINE([HAVE_SQLCIPHER], [1],
                         [Define if you have sqlcipher])
               HAVE_SQLCIPHER=1
	       LIBS="$LIBS -lpthread"
              ],
             [if test "x$with_sqlcipher" != xcheck; then
                 AC_MSG_FAILURE(
                   [--with-sqlcipher was given but test failed])
               fi
             ])])
AM_CONDITIONAL(WITH_SQLCIPHER, [test "$HAVE_SQLCIPHER" = "1"])

AC_CHECK_LIB(sqlite3, sqlite3_db_filename, [with_sqlite=yes])
AM_CONDITIONAL(SQLITE, test x$with_sqlite = xyes)

if test "$with_sqlcipher" = "yes"; then
    echo "Using libsqlcipher"
elif test "$with_sqlite" = "yes"; then
    echo "Using libsqlite3"
    AC_SUBST([SQLITE], ["-lsqlite3"])
else
    echo "Error! SQLCipher or SQLite3 is required!"
    exit -1
fi

AC_SYS_LARGEFILE

# fuse?
AC_ARG_WITH([fuse],
            [AS_HELP_STRING([--with-fuse], [use FUSE library for client])],
            [],
            [with_fuse=check])
LIBFUSE=
AS_IF([test "x$with_fuse" != xno],
	        [AC_CHECK_LIB([fuse], [fuse_main],
             [AC_SUBST([LIBFUSE], ["-lfuse"])
               AC_DEFINE([HAVE_LIBFUSE], [1],
                         [Define if you have fuse])
               HAVE_LIBFUSE=1
	       CFLAGS="$CFLAGS -D_FILE_OFFSET_BITS=64 -D_REENTRANT -DFUSE_USE_VERSION=25"
	       LIBS="$LIBS -lpthread"
              ],
             [if test "x$with_fuse" != xcheck; then
                 AC_MSG_FAILURE(
                   [--with-fuse was given but test failed])
               fi
             ])])
AM_CONDITIONAL(WITH_FUSE, [test "$HAVE_LIBFUSE" = "1"])


AC_CONFIG_FILES(Makefile tests/Makefile libsqlfs.pc)

AC_OUTPUT

